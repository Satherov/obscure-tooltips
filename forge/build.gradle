import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id 'net.darkhax.curseforgegradle'
    id "com.modrinth.minotaur"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")
    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {

    // Mixin
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0"))

    // Forge
    modImplementation("curse.maven:emi-580555:6420945")
    modRuntimeOnly("curse.maven:obscure-api-638417:7171906")
    modRuntimeOnly("curse.maven:ob-aquamirae-536254:7165722")
    modRuntimeOnly("curse.maven:quality-equipment-667174:5523943")

    // Common
    modImplementation("dev.obscuria:fragmentum-forge:1.20.1-${fragmentum_version}")
    compileOnly project(":common")
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}

tasks.register("releaseForge") {
    dependsOn(tasks.named("build"))
    doLast {
        copy {
            from(file("build/libs/${mod_id}-${project.name}-${minecraft_version}-${version}.jar"))
            into(rootProject.file("output/$version"))
        }
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    dependsOn(tasks.named("releaseForge"))
    group = 'publishing'

    apiToken = System.getenv("CF_TOKEN")
    def file = rootProject.file("output/${version}/${mod_id}-${project.name}-${minecraft_version}-${version}.jar")
    def artifact = upload(curseforge_id, file)

    artifact.displayName = "${mod_name} ${project.version}"
    artifact.changelogType = "markdown"
    artifact.changelog = rootProject.file('CHANGELOG.md').text
    artifact.releaseType = release_type

    artifact.addEnvironment("Client")
    artifact.addGameVersion("Forge", minecraft_version, "Java ${java_version}")
    artifact.addRequirement("fragmentum")

    debugMode = release_debug_mode.toBoolean()
}

modrinth {

    token = System.getenv("MD_TOKEN")
    projectId = modrinth_id
    uploadFile = rootProject.file("output/${version}/${mod_id}-${project.name}-${minecraft_version}-${version}.jar")

    versionName = "${mod_name} ${project.version}"
    versionNumber = "${project.version}"
    versionType = release_type
    changelog = rootProject.file("CHANGELOG.md").text

    gameVersions = ["${minecraft_version}"]
    loaders = ["forge"]
    dependencies {
        required.project("fragmentum")
    }

    debugMode = release_debug_mode.toBoolean()
}